#!/usr/bin/expect -f

set kernel [lindex $argv 0]
set runtime [lindex $argv 1]
set launcher [lindex $argv 2]
set qemu [lindex $argv 3]
set password [lindex $argv 4]
set container_ip [lindex $argv 5]
set server_ip [lindex $argv 6]
set app [lindex $argv 7]

set timeout 300

log_file -noappend log/run_fig16_${runtime}_${app}.log


proc __start_server {} {
    global app
    global kernel
    global password
    global container_ip
    global launcher
    global qemu

    spawn bash -c "QEMU=$qemu ./scripts/simulate.sh $kernel tap"

    expect "password for"
    send "$password\n"

    expect "login: "
    send "root\n"

    expect "Password: "
    send "123\n"

    expect "root@ubuntu"
    send "cd split-kernel/tender\n"

    expect "root@ubuntu"
    send "IP=$container_ip $launcher $app\n"

    return $spawn_id
}

proc shutdown_qemu {spawn_id} {
    send "\x01"
    send "x"
    expect "QEMU: Terminated"
    sleep 2
}

proc start_server {} {
    set started 0
    while {$started != 1} {
        set spawn_id [__start_server]
        expect {
            "Run /bin/init as init process" {
                sleep 1
                set started 1
            }
            "kvm run failed Input/output error" {
                shutdown_qemu $spawn_id
            }
        }
    }
    return $spawn_id
}

proc run_benchmark {clients} {
    global password
    global server_ip
    global app

    spawn docker rm -f memtier_benchmark
    expect eof

    spawn bash -c "./scripts/run_memtier_benchmark.sh $app $clients $server_ip\n"

    expect "password for"
    send "$password\n"

    expect {
        "Benchmark done" {
            return 1
        }
        "0.00KB/sec" {
            return 0
        }
    }
}

set qemu_id [start_server]
for {set x 1} {$x<=12} {incr x} {
    if {[run_benchmark $x] != 1} {
        incr x -1
        shutdown_qemu $qemu_id
        set qemu_id [start_server]
    }
}
shutdown_qemu $qemu_id
